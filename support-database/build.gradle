apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

afterEvaluate {
    tasks.withType(Javadoc) {
        docTask -> docTask.failOnError = false
    }
    tasks.getByName("preBuild") {
        doLast {
            println("------------------afterEvaluate preBuild task doLast----------------------")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

ext {
    // installv1
    bintrayRepo = 'maven'
    bintrayName = 'support-database'

    // library
    libraryGroupId = 'com.android.support'
    libraryVersion = "${versionMajor}.${versionMinor}.${versionPatch}"
    libraryName = 'support-database'
    libraryArtifact = 'support-database'
    libraryUserOrg = 'fine1021'
    libraryDescription = 'a support database library for android'

    // url
    siteUrl = 'https://github.com/fine1021/library'
    gitUrl = 'https://github.com/fine1021/library.git'
    issueUrl = 'https://github.com/fine1021/library/issues'

    // developer
    developerId = 'fine1021'
    developerName = 'fine'
    developerEmail = 'fine1021@gmail.com'

    // licenses
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

task binaryJar(dependsOn: ['compileReleaseJavaWithJavac'], type: Jar) {

    baseName = 'support-database'
//    appendix = "bridge"
    version = "${versionMajor}.${versionMinor}.${versionPatch}"
//    classifier = "release"
    extension = "jar"

    // [baseName]-[appendix]-[version]-[classifier].[extension]
    // archiveName = "support-database.jar"

    def srcClassDir = [project.buildDir.absolutePath + "/intermediates/classes/release"];
    from srcClassDir

    exclude "**/BuildConfig.class"
    exclude "**/R.class"
    exclude "**/R\$*.class"
}

task sourcesJar(type: Jar) {

    baseName = 'support-database'
//    appendix = "bridge"
    version = "${versionMajor}.${versionMinor}.${versionPatch}"
    classifier = "sources"
    extension = "jar"

    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    baseName = 'support-database'
//    appendix = "bridge"
    version = "${versionMajor}.${versionMinor}.${versionPatch}"
    classifier = "javadoc"
    extension = "jar"

    from javadoc.destinationDir
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publish {
    userOrg = libraryUserOrg
    repoName = bintrayRepo
    groupId = libraryGroupId
    artifactId = libraryArtifact
    publishVersion = libraryVersion
    description = libraryDescription
    website = 'https://github.com/fine1021/library'
    issueTracker = "${website}/issues"
    repository = "${website}.git"
    bintrayUser = properties.getProperty("bintray.user")
    bintrayKey = properties.getProperty("bintray.apikey")
    dryRun = false
}

