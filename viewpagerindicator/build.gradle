apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 16
    buildToolsVersion "19.1.0"

    defaultConfig {
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    lintOptions {
        abortOnError false
        ignoreWarnings true
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
}

def PROJ_GROUP="cn.edu.nuaa.Sunday"
def PROJ_VERSION="1.0.0"
def PROJ_NAME="viewpagerindicator"
def PROJ_WEBSITEURL="https://github.com/fine1021/library"
def PROJ_VCSURL="https://github.com/fine1021/library.git"
def PROJ_ISSUETRACKERURL="https://github.com/fine1021/library/issues"
def PROJ_DESCRIPTION="viewpagerindicator"
def DEVELOPER_ID="fine1021"
def DEVELOPER_NAME="fine"
def DEVELOPER_EMAIL="fine1021@gmail.com"

version = PROJ_VERSION
group = PROJ_GROUP

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                packaging 'aar'
                // Add your description here
                name PROJ_DESCRIPTION
                url PROJ_WEBSITEURL
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = PROJ_NAME
        desc = PROJ_DESCRIPTION
        websiteUrl = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        issueTrackerUrl = PROJ_ISSUETRACKERURL
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
    }
}


