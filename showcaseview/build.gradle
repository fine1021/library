/*
 * Copyright 2014 Alex Curran
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

dependencies {
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
    }
}

def PROJ_GROUP="cn.edu.nuaa.Sunday"
def PROJ_VERSION="1.0.2"
def PROJ_NAME="showcaseview"
def PROJ_WEBSITEURL="https://github.com/fine1021/library"
def PROJ_VCSURL="https://github.com/fine1021/library.git"
def PROJ_ISSUETRACKERURL="https://github.com/fine1021/library/issues"
def PROJ_DESCRIPTION="showcaseview"
def DEVELOPER_ID="fine1021"
def DEVELOPER_NAME="fine"
def DEVELOPER_EMAIL="fine1021@gmail.com"

group = PROJ_GROUP
version = PROJ_VERSION

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name PROJ_DESCRIPTION
                url PROJ_WEBSITEURL
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = PROJ_NAME
        desc = PROJ_DESCRIPTION
        websiteUrl = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        issueTrackerUrl = PROJ_ISSUETRACKERURL
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
    }
}

